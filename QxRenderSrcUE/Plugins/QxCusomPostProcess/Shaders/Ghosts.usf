#include "Shared.ush"

float4 GhostColors[8];
float GhostScales[8];
float Instensity;

void GhostPS(
    in noperspective float4 UVAndScreenPos : TEXCOORD0,
    out float4 OutColor : SV_TARGET0
    )
{
    float2 UV = UVAndScreenPos.xy;

    float3 Color = float3(0.f, 0.f, 0.f);

    UNROLL
    for (int i = 0; i < 8; ++i)
    {
        // 跳过当前ghost 如果其size 接近0
        if ( abs(GhostColors[i].a * GhostScales[i]) > 0.0001f)
        {
            float2 NewUV = (UV - 0.5f) * GhostScales[i];

            //Local Mask
            float DistanceMask = 1.0f - distance( float2(0.f, 0.f), NewUV);
            float Mask = smoothstep(0.5f, 0.9f, DistanceMask);
            float Mask2 = smoothstep(0.75f, 1.0f, DistanceMask) * 0.95f + 0.05f;

            Color += Texture2DSample(InputTexture, InputTextureSampler, NewUV + 0.5f).rgb
                    * GhostColors[i].rgb
                    * GhostColors[i].a
                    * Mask * Mask2;
        }
    }
    
    float2 ScreenPos = UVAndScreenPos.zw;
    float ScreenBorderMask = DiscMask(ScreenPos * 0.9f);

    OutColor.rgb = Color * ScreenBorderMask * Instensity;
    OutColor.a = 0;
}